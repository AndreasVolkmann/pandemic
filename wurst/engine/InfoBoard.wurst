package InfoBoard
import ClosureTimers
import User
import TurnStatus
import Constants
import Infection

let columns = 2
let rows = 6
multiboard b

public function initializeMultiBoard()
    doAfter(0.25) ->
        b = CreateMultiboard()
            ..setRowCount(rows)
            ..setColumnCount(columns)
            ..setTitle("Pandemic")
        updateLabel(0, "Current Player:")
        updateLabel(1, "Phase:")
        updateLabel(2, "Turn:")
        updateValue(2, "0")
        updateLabel(3, "Infection Rate:")
        updateValue(3, infectionRateStart.toString())
        updateLabel(4, "Outbreaks: ")
        updateValue(4, "0")
        updateLabel(5, "Cards left in deck: ")
        updateValue(5, "0")
        b.setItemsWidth(0.10)
        b.setItemsStyle(true, false)
        b.display(true)
    //CreateTimer().startPeriodic(0.5, () -> update())

public function updateUser(User user)
    updateValue(0, user.name)

public function updatePhase(TurnStatus phase)
    updateValue(1, phase.getName())

public function updateTurn(int turn)
    updateValue(2, turn.toString())

public function updateInfection(Infection infection)
    updateValue(3, infection.infectionRate.toString())
    updateValue(4, infection.numOutbreaks.toString())

public function updateCardsLeftInDeck(int amount)
    updateValue(5, amount.toString())

function updateLabel(int row, string value)
    b.getItem(row, 0).setValue(value)

function updateValue(int row, string value)
    b.getItem(row, 1).setValue(value)