package Game

import City
import Infection
import LinkedList
import Util
import Card
import CardFactory
import User
import InfoBoard

public class Game
    let users = new LinkedList<User>()
    var turnNumber = 0

    var diseasesCured = 0
    let maxDiseases = 4
    let maxOutBreaks = 8
    let minPlayerCards = 2 // If, as you are about to draw, there are fewer than 2 cards left in the Player Deck, the game ends and your team has lost!

    private LinkedList<Card> deck
    Infection infection

    construct(LinkedList<player> players)
        let cityKeys = City.cities.getKeys()
        deck = generateDeck(cityKeys)
            
        infection = new Infection(cityKeys)
        let numPlayers = players.size()
        var numInitialCards = 4
        if numPlayers == 3
            numInitialCards = 3
        else if numPlayers >= 4
            numInitialCards = 2
        players
            .map(p -> new User(p))
            .forEach(u -> users.add(u))
            .forEach(u -> draw(u, numInitialCards))

        // after initial cards have been drewn, add epidemic cards to deck
        addEpidemicCards(deck)

    function start()
        infection.initialInfection()
        turnNumber++

    function draw(User user)
        draw(user, 2)

    private function draw(User user, int amount)
        repeat(amount) i ->
            let card = deck.pop()
            user.hand.add(card)
            Log.info("Drew: " + card.getName())
        updateCardsLeftInDeck(deck.size())

    function infect()
        infection.infect()

    function check() returns boolean
        if diseasesCured == maxDiseases
            endGame(true, "All diseases cured.")
        else if infection.numOutbreaks == maxOutBreaks
            endGame(false, "Too many outbreaks")
        else if deck.size() < minPlayerCards
            endGame(false, "Player deck is empty")
        else if infection.diseaseSpreadTooMuch()
            endGame(false, "Diseases spread too much")
        else   
            return false
        return true

    function endGame(boolean won, string reason) returns boolean
        if won
            Log.info("Game won!")
        else
            Log.info("Game lost. " + reason)
        return true
