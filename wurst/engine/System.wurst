package System
import User
import LinkedList
import InfoBoard
import Game
import Movement
import Constants
import TurnStatus

public let system = new System()

public class System
    Game game
    private LLIterator<User> userIterator
    private let turnOrder = asList(TurnStatus.Start, TurnStatus.Move, TurnStatus.Draw, TurnStatus.Infect, TurnStatus.End).staticItr()
    var actions = maxActions

    function getCurrentUser() returns User
        return userIterator.current.elem

    function start(LinkedList<player> players)
        game = new Game(players)..start()
        userIterator = game.users.staticItr()
        userIterator.next()
        next()

    private function getNextStatus() returns TurnStatus
        let current = turnOrder.current.elem
        if current == TurnStatus.Move and actions > 0
            return current
        else 
            actions = maxActions
            return turnOrder.next()
        
    private function next()
        if game.check()
            return // end of game
        let status = getNextStatus()
        let user = getCurrentUser()
        updateUser(user)
        updatePhase(status)

        switch status
            case TurnStatus.Start
                Log.info("It is " + user.name + "'s turn")
                updateTurn(game.turnNumber)
                next()
            case TurnStatus.Move
                Log.info("Actions left: " + actions.toString())
                enableMovement(user)
                actions--
            case TurnStatus.Draw
                game.draw(user)
                next()
            case TurnStatus.Infect
                Log.info("Infection")
                game.infect()
                updateInfection(game.infection)
                next()
            case TurnStatus.End
                Log.info("Turn has ended")
                disableMovement(user)
                if not userIterator.hasNext()
                    userIterator.reset()
                userIterator.next()
                turnOrder.reset()
                game.turnNumber++
                next()

    function move(unit destination, boolean isCard)
        getCurrentUser().move(destination, isCard)
        next()

    function treat()
        let user = getCurrentUser()
        let loc = user.currentLocation
        Log.info("Treating infection in " + loc.name)
        loc.treat()
        next()

    function cure()
        next()

    function build()
        next()

    function share()
        next()
    
    function pass()
        next()
